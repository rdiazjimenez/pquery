// Autodetects column types in table (based on https://social.technet.microsoft.com/Forums/en-US/5883f1cd-0665-45b5-92bf-ccf3489d37c7/transform-column-types-dynamically-via-pq?forum=powerquery)

(table as table, optional culture as nullable text) as table =>
let
    InvalidTypes = {type list, type record, type table, type function, type type, type null},
    Top200Rows = Table.FirstN(table, 200), //we use up to 200 rows to establish a column type
    ColumnNameList = Table.ColumnNames(Top200Rows),
    ColumnDataLists = List.Accumulate(ColumnNameList, {}, (accumulated, i) => accumulated & {Table.Column(Top200Rows, i)}),
    ColumnTypes = List.Transform(ColumnDataLists, (i) => List.ItemType(i)),
    TransformList = List.Select(List.Zip({ColumnNameList, ColumnTypes}), (i) => 
                        not List.Contains(List.Transform(InvalidTypes, (j) => Type.Is(i{1}, j)), true)),
    TypedTable = Table.TransformColumnTypes(table, TransformList, culture),
         
        List.ItemType = (list as list) =>
        let            
            ItemTypes = List.Transform(
                            list, 
                            each if Value.Type(Value.FromText(_, culture)) = type number
                                 then if Text.Contains(Text.From(_, culture),"%") then Percentage.Type
                                      else if Text.Length(Text.Remove(Text.From(_, culture), {"0".."9"} & Text.ToList("., -+eE()/'"))) > 0 
                                               then Currency.Type
                                      else if Int64.From(_, culture) = Value.FromText(_, culture) then Int64.Type
                                      else type number
                                      else Value.Type(Value.FromText(_, culture))
                        ),
            ListItemType = Type.Union(ItemTypes)
        in
            ListItemType
in
    TypedTable
